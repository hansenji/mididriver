
plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdkVersion 28
    namespace "org.billthefarmer.mididriver"

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }

        // cmake {
        //     path 'src/main/jni/CMakeLists.txt'
        // }
    }

    ndkVersion "28.1.13356709"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
    }

    buildFeatures {
        prefab true
    }

    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lint {
        disable 'OldTargetApi', 'ExpiredTargetSdkVersion', 'GradleDependency'
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

publishing {
    publications {
        // Creates a component called "release" that contains the
        // artifacts of the release build type.
        release(MavenPublication) {
            groupId = 'com.github.billthefarmer'
            artifactId = 'mididriver'
            version = '1.27.0.2'

            afterEvaluate {
                from components.release
            }
        }
    }
}

dependencies {
    implementation 'com.google.oboe:oboe:1.6.1'
}
